module syncControl

import mobl::ui::generic
import servicemodel
import Yellowgrassmobile
import lib
import customStyle
import serviceOfflineWrapper
import progressbar

var syncFlag : Bool = false;
var lastSynced : String = "Never";
var dataComplete : String = "Unknown"; 
screen syncSettings() {
		script{
			async{
					getProjects(); 	
			}
		}
		myTabSet(
			[ 	("General", "", generalSyncSettings),	
				("Set Projects", "", syncProjects)], 
		activeTab = null, 
		screenContextId = "tabSetContext",
		tabbarStyle = MyTabbarStyle, 
		activeTabButtonStyle = MyActiveTabButtonStyle, 
		inActiveTabButtonStyle = MyInActiveTabButtonStyle, 
		activeTabStyle = activeTabStyle,
		inActiveTabStyle = inActiveTabStyle
	)
}

control syncProjects() {
	header("Sync Settings") {
		backButton("back", onclick={ root(target="_top"); })
	}
	var phrase = ""
	
	searchBox(phrase) 
	list(pr in Project.searchPrefix(phrase).order("name", true)) {
		item{ checkBox(pr.sync, label=pr.name) }
	}
}

control generalSyncSettings() {
	header("Sync Settings") {
		backButton("back", onclick={ root(target="_top"); })
	}
	script{
		checkLastSynced();
	}
	html("<h4><font color=#334E33>Last Synced</font></h4>" +  lastSynced)
	group{
		html("<h4><font color=#334E33>Data Status</font></h4>")
		label("Complete: ") 
		checkProjectsSynced()  
		<br/>
		label("Dirty: Unknown")
	}
	html("<h4><font color=#334E33>Sync Status</font></h4>")
	group{ 
		when(syncFlag){
			progressbar()
		} else {
			button("Sync now", onclick={ syncDataProjects(); })
		}
	}
}

sync function syncDataProjects() {
	resetProgress();
	syncFlag = true ;
	var start = now();
	var workdone = 0;
	var count = 1;
	var nrProjects = Project.all().filter("sync", "=", true).count();
	foreach (project in Project.all().filter("sync", "=", true)) {
		log("syncProject: " + project.name);
		setLabel("syncing : " + project.name);
		syncProject(project);
		setPersentage(100/nrProjects * count);
		count= count + 1;
	}
	var end = now();
	log("timeNeeded: " + (end - start) / 1000 + " s");
	setLastGeneralSyncFromLocalStorage(start);
	syncFlag = false;
	dataComplete = checkProjectsEverSynced();
}

sync function syncProject(project : Project) {
	// var project = Project.findBy("name", projectname);
	project.lastSynced = now();
	log("get first project info");
	getProject(project.name);
	log("getIssues"); 
	getIssues(project);
	log("getRoadMap");
	project.roadmap = getRoadmap(project);
	log("getIssueDetails");
	getIssuesDetails(project);
	log("done");
}

function checkLastSynced() {
	var generalSync = getLastGeneralSyncFromLocalStorage();
	if(!generalSync) {
		lastSynced = "Never";
	} else {
		lastSynced = formatDate(generalSync);
	}
}

function getLastGeneralSyncFromLocalStorage() : DateTime {
	var lastSync = LocalStorage.getItem("lastSync");
	if(lastSync) {
		return  DateTime.parse(lastSync);
	} else {
		return null;
	}
}

function setLastGeneralSyncFromLocalStorage(time : DateTime)  {
	lastSynced = formatDate(time);
	LocalStorage.setItem("lastSync",time.toString());
}

function checkProjectsEverSynced() : String {
	foreach (project in Project.all().filter("sync", "=", "true")) {
		if(!project.lastSynced) {
			return "Missing";
		}
	} 
	if(Project.all().filter("sync", "=", "true").count() == 0 ){
		return "no data to sync";
	}
	return "true";
}

control checkProjectsSynced() {
	script{
		dataComplete = checkProjectsEverSynced();
	}
	
	when(dataComplete == "Missing") {
		image("images/kruisje.png")
	} else {
		when(dataComplete == "true") {
			image("images/tick.png")
		} else {
			label(dataComplete)
		}
	}
}
